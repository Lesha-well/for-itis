// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    
    ALU(
        x=Dout,             // Dout - значение регистра D
        y=AMout,            // AMout - либо значение M = R[A] либо A
        zx=instruction[11], // Описание операции
        nx=instruction[10], 
        zy=instruction[9],
        ny=instruction[8],
        f=instruction[7],
        no=instruction[6],
        out=ALUout,         // Результат
        out=outM,           // M = результат
        zr=isZero,
        ng=isNeg
        ); 

    Not(in=instruction[15], out=Ainstruction); // Контрольный бит A/C инструкция

    And(a=instruction[15], b=instruction[5], out=ALUtoA); // если С инструкция и надо записать в A
    Mux16(a=instruction, b=ALUout, sel=ALUtoA, out=Aregin); // A = A, либо A = ALUout если ALUtoA

    // запись в А
    Or(a=Ainstruction, b=ALUtoA, out=loadA);  // если А инструкция или если С инструкция и надо записать в A
    Register(in=Aregin, load=loadA, out=Aout, out[0..14]=addressM); // запись в A или остается старое значение

    // если a-бит в C инструкции == 1, то ALU оперирует над D и M, иначе - над D и A
    Mux16(a=Aout, b=inM, sel=instruction[12], out=AMout);  

    // запись в D
    And(a=instruction[15], b=instruction[4], out=loadD); // если С инструкция и надо записать в D
    DRegister(in=ALUout, load=loadD, out=Dout);   // запись в D или остается старое значение
    
    // если C инструкция и надо записать в M
    And(a=instruction[15], b=instruction[3], out=writeM);
    
    // работа с счетчиком
    And(a=isZero, b=instruction[1], out=jeq);   // if == 0
    And(a=isNeg, b=instruction[2], out=jlt);    // if < 0
    Or(a=isZero, b=isNeg, out=zeroOrNeg);       // if <= 0
    Not(in=zeroOrNeg, out=positive);            // not( <= ) = ( > )
    And(a=positive, b=instruction[0], out=jgt); // if > 0
    Or(a=jeq, b=jlt, out=jle);
    Or(a=jle, b=jgt, out=jumpToA);              // Объединяем условия прыжка
    
    // Бит загрузки == ( C инструкция AND выполнены условия прыжка )
    And(a=instruction[15], b=jumpToA, out=PCload);
    // Если load, то прыжок на адрес Aout, иначе pc += 1
    PC(in=Aout, inc=true, load=PCload, reset=reset, out[0..14]=pc);
}